# Maintainer: {{ cookiecutter.author_full_name }} <{{ cookiecutter.author_email }}>

pkgname={{ cookiecutter.package_name }}-git
pkgver=VERSION
pkgrel=1
pkgdesc='{{ cookiecutter.project_description }}'
arch=('any')
url='https://github.com/{{ cookiecutter.github_username }}/{{ cookiecutter.package_name }}'
license=('{{ cookiecutter.spdx_license }}')
depends=(
  'python-colorama'
  'python-fire'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-poetry'
  'python-wheel'
)
checkdepends=('python-pytest')
provides=('{{ cookiecutter.package_name }}')
conflicts=('{{ cookiecutter.package_name }}')
options=('!strip')
source=("git+file://$(git rev-parse --show-toplevel)")
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/{{ cookiecutter.project_slug }}"
  printf "%s.r%s.%s" \
    "$(poetry version -s)" \
    "$(git rev-list --count HEAD)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/{{ cookiecutter.project_slug }}"
  python -m build --wheel --no-isolation
}

check() {
  cd "${srcdir}/{{ cookiecutter.project_slug }}"
  python -m pytest
}

package() {
  cd "${srcdir}/{{ cookiecutter.project_slug }}"

  echo >&2 'Packaging the wheel'
  python -I -m installer --destdir="${pkgdir}" dist/*.whl

  echo >&2 'Packaging README.md'
  install -D -m 644 -t "${pkgdir}/usr/share/doc/${pkgname}" \
    'README.md'

  echo >&2 'Packaging the license'
  install -D -m 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" \
    'LICENSE'
}
